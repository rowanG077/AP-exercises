module Fractal_tier2;

use Structures: all;
use Numerical: all;
use StdIO: all;

export all;

/** <!--*******************************************************************-->
 *
 * @fn int, complex escapeTimeAndValue(complex z, int depth)
 *
 *   @brief iteratively computes c = c*c + z starting from c=z.
 *          terminates either when |c| > 2 or depth iterations
 *          have been performed.
 *          NB: Besides "norm" there exists "normSq" which computes |c|^2.
 *
 *   @return number of iterations until termination and final complex value
 *****************************************************************************/
int, complex escapeTimeAndValue(complex z, int depth)
{
  it = 0;
  c = z;
  while (norm(c) <= 2.0 && it < depth) {
    c = c * c + z;
    it += 1;
  }
  return (it, c);
}

/** <!--*******************************************************************-->
 *
 * @fn int[*], complex[*] escapeTimeAndValue(complex[*] z, int depth)
 *
 *   @brief maps escapeTimeAndValue to an entire array of complex numbers
 *
 *****************************************************************************/
inline
int[.,.], complex[.,.] escapeTimeAndValue( complex[.,.] plane, int depth)
{
  its =  with {
    (. <= iv <= .) {
      it, c = escapeTimeAndValue(plane[iv], depth);
    } : it;
  } : genarray(shape(plane), 0);

  cs =  with {
    (. <= iv <= .) {
      it, c = escapeTimeAndValue(plane[iv], depth);
    } : c;
  } : genarray(shape(plane), toc(0.0, 0.0));

  return (its, cs);
}


/** <!--*******************************************************************-->
 *
 * @fn double normalizedIterationCount(int n, complex zn)
 *
 *   @brief normalizes the iteration counts in "n" taking the distance of the 
 *          corresponding final complex numbers from the origin into account.
 *          For corresponding elements in "n" and "zn", we have:
 *             0.0                          iff |zn| <= 2
 *             (n+1) - log2( log2( |zn|))   otherwise
 *
 *   @return the normalized iteration counts
 *****************************************************************************/
inline
double[*] normalizedIterationCount(int[*] n, complex[*] zn)
{
   return with {
     (. <= iv <= .) {
       it = tod(n[iv]);
       normz = norm(zn[iv]);
       ret = 0.0;
       if (normz > 2.0) {
         ret = (it + 1.0) - log2(log2(normz));
       }
     } : ret;
   } : genarray(shape(zn), 0.0);
}

double mapRange(double x, double inMin, double inMax, double outMin, double outMax) {
  outRange = outMax - outMin;
  inRange  = inMax - inMin;
  return (x - inMin) * outRange / inRange + outMin;
}

Color8::color HsbAngleToRgb(double angle)
{
  return Color8::Hsb2Rgb(toi(angle), 60, 80);
}

/** <!--*******************************************************************-->
 *
 * @fn color[.,.] doubleArrayToRGB( double[.,.] a)
 *
 *   @brief transforms an array of doubles into RGB values, by 
 *          FIRST scaling them into values between 0.0 and 360.0, and
 *          THEN  using them as HSB angles to choose a color.
 *          NB: Color8 provides a function Hsb2Rgb for the actual
 *              conversion.
 *  
 *   @return RGB values
 *****************************************************************************/
inline
Color8::color[.,.] doubleArrayToRGB( double[.,.] a)
{
  minValue = minval(a);
  maxValue = maxval(a);

  return with {
    (. <= iv <= .) : (HsbAngleToRgb(mapRange(a[iv], minValue, maxValue, 0.0, 360.0)));
  } : genarray(shape(a), Color8::black());
}
